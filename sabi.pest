WHITESPACE = _{ " " | "\t" | NEWLINE }

// The capsule for the program and the 
//  enum for its statements
act = { SOI ~ scene+ ~ EOI }
scene = { "SCENE " ~ scene_num ~ statement* ~ "CURTAIN" }

// There are three types of statements
//  1. Code statements for logic, etc
//  2. Stage directions for changing scenes, backgrounds, sounds, etc
//  3. Dialogue for characters to say things
statement = _{
    code |
    stage |
    dialogue
    }

// Stage directions
stage = { "(" ~ stage_command ~ ")" }
    stage_command = _{ background_change }
    background_change = { "Background changes to " ~ string }

// Code statements
code = { "{" ~ code_statement ~ "}" }
    code_statement = _{ log }
    // Writes a message to the console
    log = { "log " ~ expr+ }

// Makes a character or the MC say something
dialogue = { character_identifier ~ ": " ~ emotion_change? ~ expr }
    emotion_change = { "(" ~ emotion_name ~ ")" }

// Expressions
expr = { term ~ (infix_op ~ term)* }
    term = _{ 
        number | 
        string | 
        "(" ~ expr ~ ")" 
        }
    infix_op = _{ add }
    add = { "+" }

// General types
character_identifier = { 
    mc_identifier |
    character_name
    }
character_name = @{ ASCII_ALPHA_UPPER ~ ASCII_ALPHA_LOWER+ }
emotion_name = @{ ASCII_ALPHA+ }
mc_identifier = { "MC" } 

// Intrinsic types
scene_num = @{ ASCII_DIGIT+ }
number    = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? }
string    = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }